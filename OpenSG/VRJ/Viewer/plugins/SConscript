#!python

import os.path

Import('baseEnv optimize')
Import('GetPlatform')

class Plugin:
   def __init__(self, name, srcs, useDebug = False):
      self.name = name

      if useDebug:
         self.name += '_d'

      self.srcs = srcs

   def getName(self):
      return self.name

   def getSources(self):
      return self.srcs

env = baseEnv.Copy()

if GetPlatform() == 'mac':
   env.Replace(SHLINKFLAGS = '-bundle')

use_debug = (optimize != 'yes')
plugins = [Plugin('SimpleNavPlugin', ['SimpleNavPlugin.cpp'], use_debug),
           Plugin('WandNavPlugin', ['WandNavPlugin.cpp'], use_debug),
           Plugin('CenterPointGrabPlugin', ['CenterPointGrabPlugin.cpp'],
                  use_debug),
           Plugin('PointGrabPlugin', ['PointGrabPlugin.cpp'], use_debug),
           Plugin('ModeSwitchPlugin', ['ModeSwitchPlugin.cpp'], use_debug),
           Plugin('ViewpointsPlugin', ['ViewpointsPlugin.cpp'], use_debug)
          ]

env.Append(CPPPATH = [os.path.join('..', '..', '..', '..')])
env.Append(LIBPATH = [os.path.join('..', 'IOV')])
env.Prepend(LIBS = ['iov'])

for p in plugins:
   env.SharedLibrary(p.getName(), p.getSources(), SHLIBPREFIX = '')
